file                        = _{ SOI ~ item* ~ EOI }

item                        =  { struct_decl | function | const_item }
const_item                  =  { const_keyword ~ ident ~ typespec ~ assign ~ expr ~ semicolon }

ident                       = @{ (XID_START | low_line | "$") ~ XID_CONTINUE* }

/// Whitespace and Comments
WHITESPACE                  = _{ (" " | "\t" | NEWLINE)+ }
COMMENT                     = _{ comment_block | comment_line }
comment_block               = @{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
comment_line                = @{ "//" ~ (!NEWLINE ~ ANY)* }

/// Keywords
let_leyword                 =  { "let" }
mut_keyword                 =  { "mut" }
const_keyword               =  { "const" }
return_keyword              =  { "return" }
unsafe_keyword              =  { "unsafe" }
if_keyword                  =  { "if" }
else_keyword                =  { "else" }
while_keyword               =  { "while" }
for_keyword                 =  { "for" }
in_keyword                  =  { "in" }
fn_keyword                  =  { "fn" }
as_keyword                  =  { "as" }
struct_keyword              =  { "struct" }

/// Bindings and accesses
binding                     =  { destructure | (mut_keyword? ~ ident) }

destructure                 = _{ array_destructure | tuple_destructure | struct_destructure }

/// Types
typespec                    =  { colon ~ ty }

ty                          = _{ array_ty | tuple_ty | ptr_ty | ident }
ptr_ty                      =  { dereference ~ (const_keyword | mut_keyword) ~ ty }

/// Functions

// A code block is simply a sequence of statements with an optional expression at the end
code_block                  =  { open_block ~ stmt* ~ close_block }
unsafe_code_block           =  { unsafe_keyword? ~ code_block }

function                    =  { unsafe_keyword? ~ fn_keyword ~ ident ~ function_args ~ function_return? ~ code_block }
function_args               = _{ open_paren ~ (function_arg ~ comma)* ~ function_arg? ~ close_paren }
function_arg                =  { binding ~ typespec }
function_return             =  { "->" ~ ty }

function_call               =  { ident ~ open_paren ~ (expr ~ comma)* ~ expr? ~ close_paren }

/// Statements
stmt                        = _{ item | local | (expr ~ semicolon?) }

local                       =  { let_leyword ~ binding ~ typespec ~ (assign ~ expr)? ~ semicolon }

/// Expressions
dereferenceable             = _{ accessor_expr | ident }

expr                        =  { cast_expr | assign_expr | return_expr | while_loop | for_loop | basic_expr }

basic_expr                  = _{ infix | inner_expr }

inner_expr                  = _{ accessor_expr | accessible_expr }
// expressions which can be accessed via array indexing or field access
accessible_expr             = _{
                                  braced_expr
                                | unsafe_code_block
                                | function_call
                                | if_expr
                                | struct_construct_expr
                                | array
                                | tuple
                                | prefix
                                | literal
                                | ident
                            }

cast_expr                   =  { basic_expr ~ (as_keyword ~ ty)+ }
prefix                      =  { unary_operator ~ inner_expr }
infix                       =  { inner_expr ~ (binary_operator ~ inner_expr)+ }

braced_expr                 =  { open_paren ~ expr ~ close_paren }

field_access                =  { "." ~ (ident | decinteger) }
array_access                =  { open_square ~ expr ~ close_square }

accessor_expr               =  { accessible_expr ~ (field_access | array_access)+} // x.0.y[1].z[4].w.etc

return_expr                 =  { return_keyword ~ expr }
assign_expr                 =  { dereferenceable ~ assign_operator ~ expr }

/// Conditionals
if_expr                     =  { if_keyword ~ expr ~ code_block ~ if_else? }
if_else                     =  { else_keyword ~ (if_expr | code_block)}

/// Loops
loop_label                  =  { "'" ~ ident }
while_loop                  =  { loop_label? ~ while_keyword                    ~ expr ~ code_block }
for_loop                    =  { loop_label? ~ for_keyword ~ ident ~ in_keyword ~ expr ~ code_block }

/// Arrays
array                       = _{ array_lit | array_splat }
array_lit                   =  { open_square ~ (expr ~ (comma ~ expr)*)?     ~ close_square }
array_splat                 =  { open_square ~ expr ~ semicolon ~ expr       ~ close_square }
array_ty                    =  { open_square ~ ty   ~ semicolon ~ expr       ~ close_square }
array_destructure           =  { open_square ~ (binding ~ comma)+ ~ binding? ~ close_square }

/// Tuples
tuple                       =  { open_paren ~ (expr    ~ comma)+ ~ expr?    ~ close_paren }
tuple_ty                    =  { open_paren ~ (ty      ~ comma)+ ~ ty?      ~ close_paren }
tuple_destructure           =  { open_paren ~ (binding ~ comma)+ ~ binding? ~ close_paren }

/// Structs
struct_field                =  { ident ~ typespec }
struct_fields               = _{ (struct_field ~ comma)* ~ struct_field? }
struct_decl                 =  { struct_keyword ~ ident ~ open_block ~ struct_fields ~ close_block }

struct_construct_field      =  { ident ~ colon ~ expr }
struct_construct_fields     = _{ (struct_construct_field ~ comma)* ~ struct_construct_field? }
struct_construct_expr       =  { ident ~ open_block ~ struct_construct_fields ~ close_block }

struct_destructure_field    =  { ident ~ (colon ~ binding)? }
struct_destructure_fields   = _{ (struct_destructure_field ~ comma)* ~ struct_destructure_field? }
struct_destructure          =  { ident ~ open_block ~ struct_destructure_fields ~ close_block }

/// Basic symbols
colon                       = { ":" }
semicolon                   = { ";" }
comma                       = { "," }
open_block                  = { "{" }
close_block                 = { "}" }
open_square                 = { "[" }
close_square                = { "]" }
open_paren                  = { "(" }
close_paren                 = { ")" }
low_line                    = { "_" }

/// Literal values
literal                     =  { float | signed_integer | boolean | string }

string                      = _{ string_single_line | raw_string }
string_single_line          = _{ string_delimiter ~ string_content ~ string_delimiter }
string_content              = ${ (string_escape | !(string_delimiter | "\\") ~ ANY)* }
string_delimiter            = _{ "\"" }
string_unicode              = _{ "u" ~ ASCII_HEX_DIGIT{4} }
string_escape               = _{ "\\" ~ ("\"" | "\\" | "a" | "b" | "f" | "n" | "r" | "t" | "v" | string_unicode) }

raw_string                  = ${
                                "r" ~ PUSH("#"*) ~ "\"" // push the number signs onto the stack
                                ~ raw_string_interior
                                ~ "\"" ~ POP            // match a quotation mark and the number signs
                            }
// unless the next character is a quotation mark
// followed by the correct amount of number signs,
// consume one character
raw_string_interior         = @{ (!("\"" ~ PEEK) ~ ANY)* }

float                       = @{ signed_integer+ ~ "." ~ integer+ ~ (^"e" ~ signed_integer+)? }

signed_integer              = _{ (plus | minus)? ~ integer }
integer                     = _{ hexinteger | bininteger | octinteger | decinteger }

decinteger                  = @{         (ASCII_DIGIT     | low_line)+ }
bininteger                  = @{ ^"0b" ~ (ASCII_BIN_DIGIT | low_line)+ }
hexinteger                  = @{ ^"0x" ~ (ASCII_HEX_DIGIT | low_line)+ }
octinteger                  = @{ ^"0o" ~ (ASCII_OCT_DIGIT | low_line)+ }

boolean                     =  { boolean_true | boolean_false }
boolean_true                =  { "true" }
boolean_false               =  { "false" }

/// Operators
reference                   =  { "&" ~ mut_keyword? }
dereference                 =  { "*" }
multiply                    =  { "*" }
divide                      =  { "/" }
modulus                     =  { "%" }
plus                        =  { "+" }
minus                       =  { "-" }
shift_left                  =  { "<<" }
shift_right                 =  { ">>" }
less_than                   =  { "<" }
less_than_or_equal          =  { "<=" }
greater_than                =  { ">" }
greater_than_or_equal       =  { ">=" }
not_equal                   =  { "!=" }
equal                       =  { "==" }
logical_and                 =  { "&&" }
logical_or                  =  { "||" }
logical_not                 =  { "!" }
bitwise_and                 =  { "&" }
bitwise_or                  =  { "|" }
bitwise_xor                 =  { "^" }
assign                      =  { "=" }
range                       =  { ".." }

unary_operator              = _{ reference | dereference | plus | minus | logical_not }
binary_operator             = _{
                                multiply | divide | modulus |
                                plus | minus |
                                shift_left | shift_right |
                                less_than_or_equal | less_than |
                                greater_than_or_equal | greater_than |
                                not_equal | equal |
                                logical_and | logical_or |
                                bitwise_and | bitwise_or | bitwise_xor |
                                range
                            }

assign_operator             =  {(
                                multiply | divide | modulus |
                                plus | minus |
                                shift_left | shift_right |
                                bitwise_and | bitwise_or | bitwise_xor
                            )? ~ "="}

